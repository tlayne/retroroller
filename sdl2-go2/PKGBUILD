# Maintainer: valadaa48

_pkgname=SDL-mirror
pkgname=sdl2-go2
pkgver=2.0.12
pkgrel=6
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard. Version 2 w/ rga rotation"
arch=('aarch64')
url="https://www.libsdl.org"
license=('MIT')
depends=('librga')
makedepends=(
    'alsa-lib'
    'mesa'
)
optdepends=('alsa-lib: ALSA audio driver')
provides=(sdl2)
conflicts=(sdl2)
source=(
  "git+https://github.com/spurious/SDL-mirror#branch=release-${pkgver}"
  "rga.patch"
)
sha512sums=('SKIP' 'SKIP')

prepare() {
  cd $srcdir/$_pkgname
  patch -p1 < ../rga.patch 
}

build() {
  cd $srcdir/$_pkgname

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-nas \
    --disable-video-x11 \
    --disable-video-opengl \
    --disable-input-tslib \
    --disable-alsatest \
    --disable-esd \
    --disable-pulseaudio-shared \
    --disable-pulseaudio \
    --disable-video-wayland \
    --disable-video-x11-xrandr \
    --disable-rpath \
    --enable-sdl-dlopen \
    --enable-video-kmsdrm

  make
}

package() {
  cd $srcdir/$_pkgname
  make DESTDIR="${pkgdir}" install
#  sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib32/cmake/SDL2/SDL2Targets-noconfig.cmake
}

# vim: ts=2 sw=2 et:

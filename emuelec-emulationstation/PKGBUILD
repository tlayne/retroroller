# Maintainer: valadaa48 <valadaa48@gmx.com>

_pkgname=emulationstation
pkgname=emuelec-emulationstation
pkgver=3526.68b6b2ca
pkgrel=3
pkgdesc="Emulation Station is a flexible emulator front-end supporting keyboardless navigation and custom system themes. EmuELEC edition."
arch=(any)
url="https://github.com/EmuELEC/emuelec-emulationstation"
license=('MIT')
depends=(
  vlc
  alsa-lib
  curl
  sdl2
  freeimage
  rapidjson
  libgo2
  sdl2_mixer
  es-theme-emuelec-carbon
)
makedepends=('cmake' 'curl' 'git')
provides=(emulationstation)
conflicts=(emulationstation)
install=emuelec-emulationstation.install
source=(
  "emulationstation::git+https://github.com/valadaa48/batocera-emulationstation#branch=rr"
  "git+https://github.com/valadaa48/EmuELEC#branch=rr"
  "emulationstation.service"
  "emulationstation.sh"
  "ee_arch"
)
md5sums=('SKIP'
         'SKIP'
         '6bf15df90c1d7e0ff1df5318dc7090ac'
         '618526cc9da2b97658a123c382de2aac'
         '34c2ed37d953249adb58c4cc96738877')
#options=(!buildflags)

pkgver() {
  cd ${_pkgname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ${_pkgname}
  git submodule update --init --recursive
}

build() {
  cd ${_pkgname}
  export CFLAGS=""
  export CXXFLAGS=""
  export LDFLAGS=""
  cmake . -DCMAKE_BUILD_TYPE=Release -DDISABLE_KODI=1 -DGLES=1 -DENABLE_EMUELEC=1 -DENABLE_FILEMANAGER=1 -DCMAKE_EXE_LINKER_FLAGS="-L/usr/lib/mali-egl -lGLESv2" -DUSE_GLES=on -DGL=off -DRPI=off -DCEC=off
  make -j17 V=1 VERBOSE=1
}

package() {
  install -Dm755 "$_pkgname/emulationstation" -t "$pkgdir/opt/emulationstation/"

  cp -r "$_pkgname/resources" "$pkgdir/opt/emulationstation/"

  install -Dm644 "$_pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/emuelec-emulationstation/LICENSE"

  install -Dm755 emulationstation.sh $pkgdir/usr/bin/emulationstation
  install -Dm755 emulationstation.service -t "$pkgdir"/usr/lib/systemd/system

  install -d "$pkgdir"/home/odroid/.emulationstation
  cp -r EmuELEC/packages/sx05re/emuelec/config/emulationstation/scripts "$pkgdir"/home/odroid/.emulationstation/

  install -d "$pkgdir"/usr/bin
  cp EmuELEC/packages/sx05re/emuelec/config/emuelec/scripts/batocera/* "$pkgdir"/usr/bin
  chmod 755 "$pkgdir"/usr/bin/*
  
  install -m644 ee_arch "$pkgdir"/ee_arch

  install -d "$pkgdir"/storage/.config
  cp -r EmuELEC/packages/sx05re/emuelec/config/emuelec "$pkgdir"/storage/.config

  chown -R odroid:odroid "$pkgdir"/storage

#  install -d "$pkgdir"/home/odroid/.config/emuelec/scripts/batocera/batocera-settings
#  cp -rv 
}


# Maintainer: valadaa48

_pkgname=RetroArch
pkgname=retroarch-odroidgo2
pkgver=53640.6c4c93e17b
pkgrel=4
pkgdesc="Retroarch for odroidgo2"
arch=('aarch64')
url="https://github.com/libretro/RetroArch"
license=('GPL')
makedepends=('git')
#options=("!strip" "debug")
depends=(
  alsa-lib
  freetype2
  glibc
  libass
  libpng
  libxkbcommon
  librga
  openal
  zlib
)
source=(
  'git+https://github.com/libretro/RetroArch#commit=6c4c93e17b'
  'retroarch.cfg'
)
backup=('etc/retroarch.cfg')
md5sums=('SKIP'
         'f0fc3ae39c9a6f15e963d4569b8ae0b5')

pkgver() {
  cd "$_pkgname"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"
  #    sed -i 's:drm/drm_fourcc.h:libdrm/drm_fourcc.h:' deps/libgo2/src/display.c gfx/drivers/oga_gfx.c
}

build() {
  export CXXFLAGS="$CXXFLAGS -fno-rtti -std=gnu++11"

  cd "$_pkgname"
  ./configure \
    --prefix=/usr \
    --disable-builtinminiupnpc \
    --disable-caca \
    --disable-discord \
    --disable-jack \
    --disable-miniupnpc \
    --disable-opengl \
    --disable-opengl1 \
    --disable-ozone \
    --disable-pulse \
    --disable-qt \
    --disable-sdl \
    --disable-sdl2 \
    --disable-tinyalsa \
    --disable-libusb \
    --disable-v4l2 \
    --disable-wayland \
    --disable-x11 \
    --disable-vulkan \
    --disable-ffmpeg \
    --disable-xvideo \
    --enable-alsa \
    --enable-egl \
    --enable-freetype \
    --enable-kms \
    --enable-odroidgo2 \
    --enable-opengles \
    --enable-opengles3 \
    --enable-udev

  make V=1
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../retroarch.cfg -t "$pkgdir"/etc
}

